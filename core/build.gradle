publishToMavenLocal.dependsOn jar

jar {
    /*from {
        (configurations.runtime).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }*/
    manifest {
        attributes 'Implementation-Title': 'chili-core',
                'Implementation-Version': project.version,
                'Main-Class': 'com.codingchili.core.Launcher'
    }
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

test {
    // if the tests needs more than this there's a memory leak.
    maxHeapSize = "512m"
    reports {
        junitXml.required = false
        html.required = true
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact rootProject.tasks.javadocJar
        }
    }
}

dependencies {
    api('io.vertx:vertx-core:4.1.5')
    api('io.vertx:vertx-web:4.1.5')

    implementation('io.dropwizard.metrics:metrics-jvm:4.2.4')
    implementation('com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.0')

    implementation('io.vertx:vertx-hazelcast:4.1.5')
    implementation('io.vertx:vertx-mongo-client:4.1.5')
    implementation('io.vertx:vertx-dropwizard-metrics:4.1.5')

    implementation('de.neuland-bfi:jade4j:1.3.2')
    implementation('de.mkammerer:argon2-jvm:2.11')
    implementation('org.fusesource.jansi:jansi:2.3.4')

    implementation('org.elasticsearch.client:elasticsearch-rest-high-level-client:7.15.0')
    implementation('com.googlecode.cqengine:cqengine:3.6.0')

    /* keep these in sync with cqengine, used internally. */
    implementation('com.esotericsoftware:kryo:5.1.1')

    /* used to hide warnings about missing slf. */
    implementation('org.slf4j:slf4j-api:1.7.30')
    implementation('org.slf4j:slf4j-simple:1.7.30')
    implementation 'org.apache.logging.log4j:log4j-core:2.14.1'

    testImplementation 'io.vertx:vertx-unit:4.1.5'
    testImplementation 'junit:junit:4.13.2'
}
