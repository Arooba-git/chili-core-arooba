apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'

project.version = "1.2.0"
project.group = 'com.github.codingchili.chili-core'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'idea'

    version = rootProject.version
    group = rootProject.group

    sourceCompatibility = 11
    targetCompatibility = 11

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    sourceSets {
        main {
            java {
                srcDir 'main/java'
            }
            resources {
                srcDir 'main/resources'
            }
        }
        test {
            java {
                srcDir 'test/java'
            }
            resources {
                srcDir 'test/resources'
            }
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    test {
        testLogging {
            exceptionFormat "full"
        }
        reports.html.enabled = false
    }
}

task alljavadoc(type: Javadoc) {
    source subprojects.collect { it.sourceSets.main.allJava }
    classpath = files(subprojects.collect { it.sourceSets.main.compileClasspath })
}

task copyJavadoc(type: Copy, dependsOn: alljavadoc) {
    from(javadoc.outputs.files)
    into 'docs/javadoc'
}

task javadocJar(type: Jar, dependsOn: alljavadoc) {
    classifier = 'javadoc'
    from alljavadoc.destinationDir
}

task testReport(type: TestReport, dependsOn: 'build') {
    destinationDir = file("$buildDir/reports/allTests")
    reportOn subprojects*.test
}

task archiveTestReport(type: Zip, dependsOn: testReport) {
    baseName = 'testreport'
    from fileTree(file("$buildDir/reports/allTests"))
}

javadoc.dependsOn javadocJar

artifacts {
    archives javadocJar
}